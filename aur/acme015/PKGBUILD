# Maintainer: Thor77 <thor77 at thor77 dot org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=python-acme015
pkgver=0.15.0
pkgrel=1
pkgdesc="ACME protocol implementation in Python"
arch=('any')
license=('Apache')
url="https://github.com/letsencrypt/letsencrypt"
makedepends=('python-setuptools' 'python-pyopenssl' 'python-pyrfc3339' 'python-pytz' 'python-requests' 'python-six' 'python-werkzeug' 'python-mock')
checkdepends=('python-nose')
source=("https://pypi.io/packages/source/a/acme/acme-$pkgver.tar.gz")
sha512sums=('dfe4082b272fbc47def699d5df0cd04910a0a5e00563d4de14edaea820201c08707ca1f4a0dfc13d0af0c95066ef3e3937dd22a323b607a46cd8ee68960d6495')

prepare() {
  sed -i "/'argparse',/d" acme-$pkgver/setup.py
}

build() {
  cd "$srcdir"/acme-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/acme-$pkgver
  python setup.py nosetests
}

package() {
  depends=('python-setuptools' 'python-pyopenssl' 'python-pyrfc3339' 'python-pytz' 'python-requests'
           'python-six' 'python-werkzeug' 'python-mock')

  cd acme-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
